plugins {
    id "fabric-loom" version "1.7-SNAPSHOT"
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "create_dragon_lib-fabric-${artifact_minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
    maven { url = "https://api.modrinth.com/maven" } // LazyDFU
    maven { url = "https://mvn.devos.one/snapshots/" } // Create, Porting Lib, Forge Tags, Milk Lib, Registrate
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // Forge Config API Port
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes
    maven { url = "https://jitpack.io/" } // Mixin Extras, Fabric ASM
    maven { url = "https://maven.tterrag.com/" } // Flywheel
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {officialMojangMappings { nameSyntheticMembers = false }}
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
    modImplementation "com.simibubi.create:create-fabric-1.18.2:${create_version}"
}

processResources {
    // require dependencies to be the version compiled against or newer
    Map<String, String> properties = new HashMap<>()
    properties.put("version", mod_version)
    properties.put("fabric_loader_version", fabric_loader_version)
    properties.put("fabric_api_version", fabric_api_version)
    properties.put("create_version", create_version)
    properties.put("minecraft_version", minecraft_version)

    properties.forEach((k, v) -> inputs.property(k, v))

    filesMatching("fabric.mod.json") {
        expand properties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = Integer.parseInt(sourceCompatibility)
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/generated/resources'
            ]
        }
    }
}

// configure the maven publication
publishing {
    publications {
        release(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
        }
        snapshot(MavenPublication) {
            artifactId = archivesBaseName
            version = "SNAPSHOT.${new Date().format('yyyyMMdd')}"
            from components.java
        }
    }
    repositories {
        maven {
            name = "release"
            url = "https://maven.dragons.plus/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
        maven {
            name = "snapshot"
            url = "https://maven.dragons.plus/snapshots"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}